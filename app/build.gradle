plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'com.google.protobuf' version '0.9.0'
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "jez.jetpackpop"
        minSdk 25
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        // Enables Jetpack Compose for this module
        compose true
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix = ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.12"
    }
    namespace 'jez.jetpackpop'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.3"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.activity:activity-compose:1.9.0'

    // compose dependencies:
    implementation(platform('androidx.compose:compose-bom:2024.04.01'))
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'androidx.compose.ui:ui-tooling'
    implementation("androidx.compose.ui:ui-tooling-preview")

    // Material Design
    implementation 'androidx.compose.material:material:1.6.6'
    implementation 'androidx.compose.material:material-icons-core:1.6.6'
    implementation 'androidx.compose.material:material-icons-extended:1.6.6'

    // ViewModel
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.7.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0")

    // Data persistence
    implementation 'androidx.datastore:datastore:1.1.0'
    implementation 'com.google.protobuf:protobuf-javalite:4.26.1'

    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.6.6'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}